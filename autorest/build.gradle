apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

// Update versionNum when releasing a new version
def versionNum = '0.7.3'
def siteUrl = 'https://github.com/Microsoft/EmbeddedSocial-Java-API-Library'
def gitUrl = 'https://github.com/Microsoft/EmbeddedSocial-Java-API-Library.git'

dependencies {
    implementation 'com.microsoft.rest:client-runtime:1.0.0-beta1'
}

ext {
    outputFolder = new File((File) project.getProjectDir(), "jar")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def pomConfig = {
    licenses {
        license {
            name 'MIT'
            url 'https://opensource.org/licenses/MIT'
        }
    }
    developers {
        developer {
            id 'acrown-msft'
            name 'Alex Crown'
            email 'acrown@microsoft.com'
        }
    }
    scm {
        url siteUrl
    }
}

publishing {
    publications {
        autorest(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.acrowntest.test'
            artifactId 'autorest'
            version versionNum
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'autorest')
                root.appendNode('description', 'test library')
                root.appendNode('url', siteUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

// Use properties to load bintray credentials
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    publications = ['autorest']
    pkg {
        repo = 'test'
        name = 'autorest'
        userOrg = 'acrowntest'
        licenses = ['MIT']
        vcsUrl = gitUrl
        publish = true
        version {
            name = versionNum
            desc = 'AutorestLib ' + versionNum
            released = new Date()
            vcsTag = versionNum
            attributes = ['gradle-plugin': 'com.acrowntest.test:autorest']
            gpg {
                sign = true
            }
        }
    }
}